onImportWizard ->
  class @Column
    constructor: (data) ->
      @header = ko.observable data.header

      # How to use this column (new field? existing field? id? name? lat? lng? ignore?)
      @usage = ko.observable data.use_as

      # For existing fields
      @layer = ko.observable(if data.layer_id then window.model.findLayer(data.layer_id) else null)
      @field = ko.observable(if @layer() then @layer().findField(data.field_id) else null)

      # For new fields
      @kind = ko.observable data.kind
      @code = ko.observable data.code
      @label = ko.observable data.label

      # For new select_one or select_many fields
      @selectKind = ko.observable 'both'

      @iconClass = ko.observable()
      @iconClass(@computeIconClass())

      @kind.subscribe  =>
        @iconClass(@computeIconClass())

      @field.subscribe =>
        @iconClass(@computeIconClass())

      @usage.subscribe =>
        # If usage change is because this column is the model.selectedColumn and fancybox it's open
        $('.fancybox').resize()
        @iconClass(@computeIconClass())

      @error = ko.observableArray([])

    toJSON: =>
      json =
        use_as: @usage()
        header: @header()
      if @usage() == 'existing_field'
        json.field_id = @field().id
      if @usage() == 'new_field'
        json.kind = @kind()
        json.code = @code()
        json.label = @label()
        json.selectKind = @selectKind() if @kind() == 'select_one' || @kind() == 'select_many'
      json

    computeIconClass: =>
      kind = switch @usage()
        when 'existing_field' then @field().kind
        when 'new_field' then  @kind()
        when 'lat' then 'location'
        when 'lng'then 'location'
        else @usage()
      "f#{kind}"